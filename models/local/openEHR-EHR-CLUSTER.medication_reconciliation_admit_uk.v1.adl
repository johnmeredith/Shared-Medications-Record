archetype (adl_version=1.4; uid=6213dce5-6884-4f15-918b-2e3daaf8fcb1)
	openEHR-EHR-CLUSTER.medication_reconciliation_admit_uk.v1

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2014-03-15">
		["name"] = <"Ian McNicoll">
		["organisation"] = <"freshEHR Clinical Informatics">
		["email"] = <"ian@freshehr.com">
	>
	lifecycle_state = <"AuthorDraft">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the result of an admission medicines reconciliation process for a single medication item.">
			keywords = <"medication","medicine","drug","admission">
			copyright = <"Â© Clinical Models UK">
			use = <"Use to record additional data captured during the process of medicines reconciliation on admission.">
		>
	>
	other_details = <
		["references"] = <"NHS National Prescribing Centre, Medicines Reconciliation, A guide to implementation. 
Available from http://www.npc.nhs.uk/improving_safety/medicines_reconciliation/resources/reconciliation_guide.pdf

Health Quality and Safety Commission New Zealand, Medicine Reconciliation Standard V3, September 2012
Available from: http://ithealthboard.health.nz/system/files/documents/publications/medication-reconciliation-standard-v3.pdf">
		["MD5-CAM-1.0.1"] = <"368248b923aa7fca18430b98db78f9a3">
		["build_uid"] = <"df8ef110-79b4-3199-ae43-778506f4e776">
	>

definition
	CLUSTER[at0000] matches {    -- Admission medication reconciliation
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0003] occurrences matches {0..1} matches {    -- Reconciliation action
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0023,    -- Add medication
							at0019,    -- Change medication
							at0020,    -- Stop medication
							at0021,    -- Withhold medication
							at0031]    -- No change to medication
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Date reconciled
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0014] occurrences matches {0..1} matches {    -- Discrepancy intentional?
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0016,    -- Intentional
							at0017]    -- Unintentional
						}
					}
				}
			}
			ELEMENT[at0028] occurrences matches {0..1} matches {    -- Target medication record updated?
				value matches {
					DV_BOOLEAN matches {*}
				}
			}
			ELEMENT[at0026] occurrences matches {0..1} matches {    -- Source of information
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0027] occurrences matches {0..1} matches {    -- Source contact details
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Link to source medication entry
				value matches {
					DV_URI matches {*}
				}
			}
			ELEMENT[at0029] occurrences matches {0..1} matches {    -- Link to target medication entry
				value matches {
					DV_URI matches {*}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Admission medication reconciliation">
					description = <"Information about admission reconciliation of a single medication entry.">
				>
				["at0003"] = <
					text = <"Reconciliation action">
					description = <"The recommendation for ongoing continuation of a medication entry within the current care setting.">
				>
				["at0006"] = <
					text = <"Date reconciled">
					description = <"The date at which the reconciliation decision was made or updated.">
				>
				["at0007"] = <
					text = <"Link to source medication entry">
					description = <"Link to the associated source medication entry to which the reconciliation recommendation refers.">
				>
				["at0014"] = <
					text = <"Discrepancy intentional?">
					description = <"Is the discrepancy was a result of a deliberate decision.">
				>
				["at0016"] = <
					text = <"Intentional">
					description = <"The discrepancy was the result of a deliberate decision.">
				>
				["at0017"] = <
					text = <"Unintentional">
					description = <"The discrepancy was not result of a deliberate decision.">
				>
				["at0019"] = <
					text = <"Change medication">
					description = <"The medication was changed, generally a dose change.">
				>
				["at0020"] = <
					text = <"Stop medication">
					description = <"The medication was stopped.">
				>
				["at0021"] = <
					text = <"Withhold medication">
					description = <"The medication has been temprarily withheld.">
				>
				["at0023"] = <
					text = <"Add medication">
					description = <"A new medication has been added.">
				>
				["at0026"] = <
					text = <"Source of information">
					description = <"The source of information about the reconciled medication. Coded terms preferred.">
				>
				["at0027"] = <
					text = <"Source contact details">
					description = <"Contact details for the person supplying source information e.g carer.">
				>
				["at0028"] = <
					text = <"Target medication record updated?">
					description = <"The target medication has neen updated.">
				>
				["at0029"] = <
					text = <"Link to target medication entry">
					description = <"A link to the target medication record.">
				>
				["at0031"] = <
					text = <"No change to medication">
					description = <"The medication has not been changed.">
				>
			>
		>
	>
